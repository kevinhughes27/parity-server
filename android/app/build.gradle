apply plugin: 'com.android.application'
//apply plugin: 'android'

android {
    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("keystore.properties")

            if (keystorePropertiesFile.exists()) {
                // Initialize a new Properties() object called keystoreProperties.
                def keystoreProperties = new Properties()

                // Load your keystore.properties file into the keystoreProperties object.
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                keyAlias 'parity'
                keyPassword keystoreProperties['keyPassword']
                storeFile file('../keystore.jks')
                storePassword keystoreProperties['storePassword']
            }
        }
    }
    compileSdkVersion 30
    buildToolsVersion '30.0.2'
    defaultConfig {
        applicationId 'io.masse.parityleaguestats'
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 7
        versionName '1.7'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.release
    }
    useLibrary 'org.apache.http.legacy'

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField "String", "UPLOAD_URL", "\"http://10.0.2.2:5000/submit_game\""
            buildConfigField "String", "LEAGUE_URL", "\"http://10.0.2.2:5000/current_league\""
            buildConfigField "String", "SCHEDULE_URL", "\"http://10.0.2.2:5000/api/%s/schedule\""
        }
    }

    flavorDimensions( "parity" )
    productFlavors {
        league {
            def baseUrl = "https://parity-server.herokuapp.com"

            buildConfigField "String", "UPLOAD_URL", "\"" + baseUrl + "/submit_game\""
            buildConfigField "String", "LEAGUE_URL", "\"" + baseUrl + "/current_league\""
            buildConfigField "String", "SCHEDULE_URL", "\"" + baseUrl + "/api/%s/schedule\""

            dimension = "parity"
        }
    }
    android.sourceSets {
        main.res.srcDirs = ['src/main/res']
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.android.support:support-annotations:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    compile 'com.android.support.constraint:constraint-layout:2.0.4'
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support:support-annotations:28.0.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
}
